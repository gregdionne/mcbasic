10 REM USING SIN AND COS
20 REM EACH TIME IN LOOP
30 CLS 0
40 FOR LOOP=1 TO 157
50 A=A+0.04
60 S=SIN(A)
70 C=COS(A)
80 SET(25*S+34, 15*C+16, 5)
90 NEXT LOOP 
100 REM USING ANGLE ADDITION
110 REM FORMULAE FOR SIN AND COS
120 CLS 0
130 DS=SIN(0.04)
140 DC=COS(0.04)
150 S=DS : C=DC
160 FOR LOOP=1 TO 157
170 SET(25*S+34, 15*C+16, 5)
180 ST = S*DC + C*DS
190 C = C*DC - S*DS
200 S = ST
210 NEXT LOOP 
300 REM USING BRESENHAM'S CIRCLE
310 R=15
315 Y=R:X=0:A=3-2*R:CLS 0
320 GOSUB370:X=X+1:B=A:A=A+4*X+6:IF B>0 THEN Y=Y-1:A=B+4*(X-Y)+10
325 IF Y>=X THEN 320
330 END
370 SET(32+X,16+Y,5):SET(32-X,16+Y,5):SET(32+X,16-Y,5):SET(32-X,16-Y,5)
375 SET(32+Y,16-X,5):SET(32-Y,16-X,5):SET(32+Y,16+X,5):SET(32-Y,16+X,5)
380 RETURN
